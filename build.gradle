import java.nio.charset.StandardCharsets

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

javadoc.options.encoding = StandardCharsets.UTF_8
compileJava.options.encoding = StandardCharsets.UTF_8
compileTestJava.options.encoding = StandardCharsets.UTF_8

group 'com.menecats'
version '1.0.1'

repositories {
    mavenCentral()
}

dependencies {
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task packageJavadoc(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Menecats/polybool-java"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = "Sonatype"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = System.getenv("SONATYPE_USERNAME")
                password = System.getenv("SONATYPE_PASSWORD")
            }
        }
    }

    publications {
        polybool(MavenPublication) {
            version = version
            groupId = group
            artifactId = rootProject.name

            from components.java
            artifact sourcesJar
            artifact packageJavadoc

            pom {
                name = 'Polybool Java'
                description = 'Boolean operations on polygons (union, intersection, difference, xor).'
                url = 'https://github.com/menecats/polybool-java'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'menecats'
                        name = 'Davide Menegatti'
                        email = 'd.menegatti@recrosoftware.com'
                    }
                }
                scm {
                    connection = 'scm:https://github.com/Menecats/polybool-java.git'
                    developerConnection = 'scm:git://github.com/Menecats/polybool-java.git'
                    url = 'https://github.com/menecats/polybool-java'
                }
            }
        }
    }

    signing {
        sign publishing.publications.polybool
    }
}